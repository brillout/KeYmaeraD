
\functions{
  R protectedzone;
}

\problem {
  \[ R x1,x2, d1,d2, omx; R xdisc1,xdisc2, ddisc1,ddisc2, xdiscom; R cx1, cx2; R cax;
     R y1,y2, e1,e2, omy; R ydisc1,ydisc2, edisc1,edisc2, ydiscom; R cy1, cy2; R cay; R minr; \] 
  (
  (
   \forall i ( \forall j (disc1(i) - disc1(j))^2 + (disc2(i) - disc2(j))^2 >= (4*minr + protectedzone)^2) &
   \forall i (c1(i) -x1(i))^2 + (c2(i) - x2(i))^2 = minr^2 &
   \forall i (c1(i) - disc1(i))^2 + (c2(i) - disc2(i))^2 = minr^2 &
   \forall i (	
	/* x in a ca maneuver */
    (ca(i) = 1 &
	 d1(i)=-om(i)*(x2(i)-c2(i)) &
	 d2(i)=om(i)*(x1(i)-c1(i))  &
	 discom(i) = 0 &
	 ddisc1(i) = 0 &
	 ddisc2(i) = 0	)
	|
	/* x NOT in ca maneuver */
	(ca(i) = 0  
	 & x1(i) = disc1(i)
	 & x2(i) = disc2(i)
	 & d1(i) = ddisc1(i)
	 & d2(i) = ddisc2(i)
	 & om(i) = discom(i))
     )
   )
  ->
     \[(((
		/* x in a ca maneuver */
		(cax := 1; omx := *; d1 := -omx*(x2 - cx2); d2 := omx*(x1-cx1);
		 cx1 := *; cx2 := *; 
		 ?((cx1 - x1)^2 + (cx2 - x2)^2 = minr^2);
		 ?((cx1 - xdisc1)^2 + (cx2 - xdisc2)^2 = minr^2);
		 xdiscom := 0;
		 ddisc1 := 0;
		 ddisc2 := 0
		 )
		++
		/* x NOT in a ca maneuver */
		(?(x1 = xdisc1 & x2 = xdisc2); cax := 0;
		 omx := *; d1 := *; d2 := *;
		 xdiscom := omx; ddisc1 := d1; ddisc2 := d2		 
		))
		 ++
		(/* y in a ca maneuver */
		(cay := 1; omy := *; e1 := -omy*(y2 - cy2); e2 := omy*(y1-cy1);
		 cy1 := *; cy2 := *; 
		 ?((cy1 - y1)^2 + (cy2 - y2)^2 = minr^2);
		 ?((cy1 - ydisc1)^2 + (cy2 - ydisc2)^2 = minr^2);
		 ydiscom := 0;
		 edisc1 := 0;
		 edisc2 := 0
		)
		++ 
		/* y NOT in a ca maneuver */
		(?(y1 = ydisc1 & y2 = ydisc2); cay := 0;
		 omy := *; e1 := *; e2 := *;
		 ydiscom := omy; edisc1 := e1; edisc2 := e2		 
		)))
		++ 
	    (
         {x1'=d1, x2'=d2, d1'=-omx*d2, d2'=omx*d1, 
		  y1'=e1, y2'=e2, e1'=-omy*e2, e2'=omy*e1, 
          xdisc1'=ddisc1, xdisc2'=ddisc2, ddisc1'=-xdiscom*ddisc2, ddisc2'=xdiscom*ddisc1,
		  ydisc1'=edisc1, ydisc2'=edisc2, edisc1'=-ydiscom*edisc2, edisc2'=ydiscom*edisc1,
		  ((cax=0 | cay=0) -> (xdisc1-ydisc1)^2 + (xdisc2-ydisc2)^2 >= (4*minr + protectedzone)^2)}
		)
	   )*@invariant((xdisc1 - ydisc1)^2 + (xdisc2 - ydisc2)^2 >= (4*minr + protectedzone)^2 &
   (cx1 - x1)^2 + (cx2 - x2)^2 = minr^2 &
   (cx1 - xdisc1)^2 + (cx2 - xdisc2)^2 = minr^2 &
   (cy1 - y1)^2 + (cy2 - y2)^2 = minr^2 &
   (cy1 - ydisc1)^2 + (cy2 - ydisc2)^2 = minr^2 &
   (
	/* x in a ca maneuver */
    (cax = 1 &
	 d1=-omx*(x2-cx2) &
	 d2=omx*(x1-cx1)  &
	 xdiscom = 0 &
	 ddisc1 = 0 &
	 ddisc2 = 0	)
	|
	/* x NOT in ca maneuver */
	(cax = 0  
	 & x1 = xdisc1
	 & x2 = xdisc2
	 & d1 = ddisc1
	 & d2 = ddisc2
	 & omx = xdiscom)
   )
   &
   (
	/* y in a ca maneuver */
    (cay = 1 &
	 e1=-omy*(y2-cy2) &
	 e2=omy*(y1-cy1)  &
	 ydiscom = 0 &
	 edisc1 = 0 &
	 edisc2 = 0	)
	|
	/* y NOT in ca maneuver */
	(cay = 0  
	 & y1 = ydisc1
	 & y2 = ydisc2
	 & e1 = edisc1
	 & e2 = edisc2
	 & omy = ydiscom)
   ));
	 \] (
       (x1-xdisc1)^2 + (x2-xdisc2)^2 <= (2*minr)^2
	   & (y1-ydisc1)^2 + (y2-ydisc2)^2 <= (2*minr)^2
	   & (xdisc1 - ydisc1)^2 + (xdisc2 - ydisc2)^2 >= (4*minr + protectedzone)^2
    )
  )
}
