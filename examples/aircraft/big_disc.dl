{
  protectedzone : () -> Real,
  minr : (C) -> Real,
  x1 : (C) -> Real,
  x2 : (C) -> Real,

  bigdisc1 : (C) -> Real,
  bigdisc2 : (C) -> Real,

  // speeds
  v : (C) -> Real,

  // unit tangent vector. velocity is v.(d1, d2)
  d1 : (C) -> Real,
  d2 : (C) -> Real,

  // -1 if we turn to the right in an avoidance maneuver
  //  1 if we turn to the left in an avoidance maneuver
  discside : (C) -> Real,

  // angular velocity
  om : (C) -> Real,
  maxom : (C) -> Real,

  // 0 if airplane is free
  // 1 if airplane is in collision avoidance maneuver
  ca : (C) -> Real,

  ii : () -> C
}

// (disc1, disc2) is point orthogonal to trajectory of aircraft
// and distance minr away.
//{
//  disc1(i) == -discside(i) * minr(i) * d2(i) + x1(i)
//  disc2(i) == discside(i) * minr(i) * d1(i) + x2(i)
//}

forall i : C .
  minr(i) > 0 & 
  d1(i)^2 + d2(i)^2 = 1,

forall i : C .
  v(i) > 0 & 
  maxom(i) > 0 &
  maxom(i) * minr(i) = v(i),

forall i : C .
   forall j : C . i /= j ==>
      (bigdisc1(i) - bigdisc1(j))^2 + (bigdisc2(i) - bigdisc2(j))^2 >= 
        (2 * minr(i) + 2 * minr(j) + protectedzone())^2,

forall i : C . (  
      (ca(i) = 0 | ca(i) = 1) 
     & (discside(i) = -1 | discside(i) = 1)
     & ((-discside(i) * minr(i) * d2(i) + x1(i) - bigdisc1(i))^2
          + (discside(i) * minr(i) * d1(i) + x2(i) - bigdisc2(i))^2 <= minr(i)^2)
     & (1 - ca(i)) * bigdisc1(i) = (1 - ca(i)) * x1(i)
     & (1 - ca(i)) * bigdisc2(i) = (1 - ca(i)) * x2(i)
     ),

protectedzone() > 0

|-   

[{ (ii := *;
      /* ii in a ca maneuver */ 
     (( (?ca(ii()) = 1);

       /* exit the maneuver */
       (( ?(bigdisc1(ii()) = x1(ii()) & bigdisc2(ii()) = x2(ii()));
         ca(ii()) := 0 ) 
        ++ 
          /* change angular velocity */
         (om(ii()) := *; ?(om(ii()) >= - maxom(ii()) & om(ii()) <= maxom(ii())))
       ))

       /* ii NOT in a ca maneuver */    
     ++ ( (?ca(ii()) = 0);
          (
           // free flight
           (om(ii()) := *; ?(om(ii()) >= - maxom(ii()) & om(ii()) <= maxom(ii()))) 
           ++
           // switch discside
           (discside(ii()) := -discside(ii()))
           ++
           // enter maneuver
           (om(ii()) := discside(ii()) * maxom(ii()); ca(ii()) := 1)
          )
      )
    )
   )
     ++
  (forall i : C
       {x1(i)' = v(i) * d1(i),
        x2(i)' = v(i) * d2(i),
        d1(i)' = -om(i) * d2(i),
        d2(i)' = om(i) * d1(i),
        bigdisc1(i)' = (1 - ca(i)) * v(i) * d1(i),
        bigdisc2(i)' = (1 - ca(i)) * v(i) * d2(i);

        // || disc(i) - bigdisc(i)|| <= minr(i)
        ((-discside(i) * minr(i) * d2(i) + x1(i) - bigdisc1(i))^2
          + (discside(i) * minr(i) * d1(i) + x2(i) - bigdisc2(i))^2 <= minr(i)^2)
        & 
        // bigdiscs are separated
        (forall j : C.
         ((i /= j &  (ca(i) = 0 | ca(j) = 0)) ==>
            ((bigdisc1(i) - bigdisc1(j))^2 + 
             (bigdisc2(i) - bigdisc2(j))^2 >= (2 * minr(i) + 2 * minr(j) + protectedzone())^2)))

            })

 }*] 
 (forall i : C .
    forall j : C .
      (i /= j ==>
        (x1(i) - x1(j))^2 + (x2(i) - x2(j))^2 >= protectedzone()^2))